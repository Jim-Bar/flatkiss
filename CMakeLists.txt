cmake_minimum_required(VERSION 3.22)

project(FlatKiss)

# Enable clang-tidy for all the targets using the .clang-tidy file.
find_program(CLANG_TIDY_IS_AVAILABLE clang-tidy)
if(CLANG_TIDY_IS_AVAILABLE)
    set(CMAKE_CXX_CLANG_TIDY clang-tidy)
else()
    message(WARNING "Linter rules are disabled because clang-tidy was not found")
endif()

# On some systems (hello macOS!) this directory is not included, so manually add it.
include_directories(/usr/local/include)

configure_file(
    source/main/src/main.hpp.in
    source/main/src/main.hpp
)

# Files generated by `configure_file()` are put in this directory, so include it.
include_directories(${CMAKE_CURRENT_BINARY_DIR}/source/main/src)

find_package(SDL2 REQUIRED)
include_directories(${SDL2_INCLUDE_DIRS})

# List of the targets to build.
set(EXECUTABLE ${PROJECT_NAME})
set(NAME_MEDIA media)
set(LIBRARY_MEDIA ${EXECUTABLE}-${NAME_MEDIA})
set(NAME_MODEL model)
set(LIBRARY_MODEL ${EXECUTABLE}-${NAME_MODEL})

# Including header files in the targets because from Modern CMake: "The headers will be, for most intents and purposes,
# ignored; the only reason to list them is to get them to show up in IDEs". Refer to:
# https://cliutils.gitlab.io/modern-cmake/chapters/basics.html

add_executable(${EXECUTABLE}
    source/main/src/character_controller.cpp
    source/main/src/character_controller.hpp
    source/main/src/configuration.cpp
    source/main/src/configuration.hpp
    source/main/src/main.cpp
    source/main/src/main.hpp
)
target_link_libraries(${EXECUTABLE} ${LIBRARY_MODEL})
target_link_libraries(${EXECUTABLE} ${LIBRARY_MEDIA})

add_library(${LIBRARY_MEDIA} STATIC
    source/${NAME_MEDIA}/src/keyboard_state.cpp
    source/${NAME_MEDIA}/src/keyboard_state.hpp
    source/${NAME_MEDIA}/src/renderer.cpp
    source/${NAME_MEDIA}/src/renderer.hpp
    source/${NAME_MEDIA}/src/texture.cpp
    source/${NAME_MEDIA}/src/texture.hpp
)
target_include_directories(${LIBRARY_MEDIA} PUBLIC
    source/${NAME_MEDIA}/include
)
target_link_libraries(${LIBRARY_MEDIA} ${LIBRARY_MODEL})
target_link_libraries(${LIBRARY_MEDIA} ${SDL2_LIBRARIES})

add_library(${LIBRARY_MODEL} STATIC
    source/${NAME_MODEL}/src/action.cpp
    source/${NAME_MODEL}/src/action.hpp
    source/${NAME_MODEL}/src/action_sprite_mapper.cpp
    source/${NAME_MODEL}/src/action_sprite_mapper.hpp
    source/${NAME_MODEL}/src/animation.cpp
    source/${NAME_MODEL}/src/animation.hpp
    source/${NAME_MODEL}/src/animation_player.cpp
    source/${NAME_MODEL}/src/animation_player.hpp
    source/${NAME_MODEL}/src/cardinal_direction.cpp
    source/${NAME_MODEL}/src/cardinal_direction.hpp
    source/${NAME_MODEL}/src/character.cpp
    source/${NAME_MODEL}/src/character.hpp
    source/${NAME_MODEL}/src/collider.cpp
    source/${NAME_MODEL}/src/collider.hpp
    source/${NAME_MODEL}/src/ellipse.cpp
    source/${NAME_MODEL}/src/ellipse.hpp
    source/${NAME_MODEL}/src/level.cpp
    source/${NAME_MODEL}/src/level.hpp
    source/${NAME_MODEL}/src/navigator.cpp
    source/${NAME_MODEL}/src/navigator.hpp
    source/${NAME_MODEL}/src/position.cpp
    source/${NAME_MODEL}/src/position.hpp
    source/${NAME_MODEL}/src/positioned_ellipse.cpp
    source/${NAME_MODEL}/src/positioned_ellipse.hpp
    source/${NAME_MODEL}/src/positioned_rectangle.cpp
    source/${NAME_MODEL}/src/positioned_rectangle.hpp
    source/${NAME_MODEL}/src/positioned_solid.cpp
    source/${NAME_MODEL}/src/positioned_solid.hpp
    source/${NAME_MODEL}/src/rectangle.cpp
    source/${NAME_MODEL}/src/rectangle.hpp
    source/${NAME_MODEL}/src/solid.cpp
    source/${NAME_MODEL}/src/solid.hpp
    source/${NAME_MODEL}/src/spriteset.cpp
    source/${NAME_MODEL}/src/spriteset.hpp
    source/${NAME_MODEL}/src/tile_solid_mapper.cpp
    source/${NAME_MODEL}/src/tile_solid_mapper.hpp
    source/${NAME_MODEL}/src/vector.cpp
    source/${NAME_MODEL}/src/vector.hpp
)
target_include_directories(${LIBRARY_MODEL} PUBLIC
    source/${NAME_MODEL}/include
)

# Setting the C++ version, from Modern CMake:
# https://cliutils.gitlab.io/modern-cmake/chapters/features/cpp11.html
target_compile_features(${EXECUTABLE} PUBLIC cxx_std_20)
set_target_properties(${EXECUTABLE} PROPERTIES CXX_EXTENSIONS OFF)
target_compile_features(${LIBRARY_MODEL} PUBLIC cxx_std_20)
set_target_properties(${LIBRARY_MODEL} PROPERTIES CXX_EXTENSIONS OFF)
target_compile_features(${LIBRARY_MEDIA} PUBLIC cxx_std_20)
set_target_properties(${LIBRARY_MEDIA} PROPERTIES CXX_EXTENSIONS OFF)
